# Mike's zshrc â€” Personal machine
# Source: ~/Development/dotfiles/configs/shell/zshrc_personal

DOTFILES_DIR="${0:A:h:h:h}"  # Resolve to dotfiles repo root

# --- Shell options ---
set -o vi
setopt RM_STAR_SILENT
setopt auto_cd
typeset -U path PATH

export EDITOR="vim"
export VISUAL="vim"
[[ -f "$HOME/.env" ]] && source "$HOME/.env"
export PATH="/opt/homebrew/bin:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin:$PATH"

if [ $(ulimit -n) -lt 8192 ]; then ulimit -n 8192; fi

# --- PATH helpers ---
function append_to_path  { [[ -d "$1" ]] && path+=$1; }
function prepend_to_path { [[ -d "$1" ]] && path=($1 $path); }

# --- Completions (cached) ---
if [[ -n "${HOMEBREW_PREFIX}" ]]; then
  FPATH="${HOMEBREW_PREFIX}/share/zsh/site-functions:${FPATH}"
fi
autoload -Uz compinit
_zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
if [[ ! -f "$_zcompdump" ]] || [[ -n $(find "$_zcompdump" -mtime +1 2>/dev/null) ]]; then
  compinit
else
  compinit -C
fi
unset _zcompdump

# --- Oh My Zsh ---
export ZSH="$HOME/.oh-my-zsh"
export ZSH_CUSTOM="$ZSH/custom"
export ZSH_CACHE_DIR="$ZSH/cache"
export ZSH_COMPDUMP="$ZSH/cache/.zcompdump"
export DISABLE_UPDATE_PROMPT=true
export DISABLE_AUTO_UPDATE=true
ZSH_THEME=""
plugins=(git sudo alias-finder jsontools)
source "$ZSH/oh-my-zsh.sh"

# --- Oh My Posh ---
command -v oh-my-posh &>/dev/null && \
  eval "$(oh-my-posh init zsh --config ~/.oh-my-posh/themes/gruvbox.omp.json)"

# --- Personal aliases ---
[[ -f "$HOME/.aliases" ]] && source "$HOME/.aliases"
[[ -f "$HOME/.localaliases" ]] && source "$HOME/.localaliases"

# --- Development profile (sourced from repo) ---
source "$DOTFILES_DIR/configs/shell/development_profile"

# --- mise (version management for Ruby, Node, Python) ---
command -v mise &>/dev/null && eval "$(mise activate zsh)"

# --- Custom git function (main/master detection) ---
function git() {
  if [[ "$1" == "checkout" && ("$2" == "main" || "$2" == "master") && $# -eq 2 ]]; then
    local default_branch=$(command git symbolic-ref refs/remotes/origin/HEAD 2>/dev/null | sed 's@^refs/remotes/origin/@@')
    if [[ -z "$default_branch" ]]; then
      if command git show-ref --verify --quiet refs/heads/main; then default_branch="main"
      elif command git show-ref --verify --quiet refs/heads/master; then default_branch="master"
      else default_branch="main"; fi
    fi
    command git checkout "$default_branch"
  else
    command git "$@"
  fi
}
